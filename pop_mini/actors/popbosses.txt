actor SkeletonBossSpawner 16
{ 
	+SOLID
	speed 0
	states
	{
		Spawn:
			BONE A 2 A_Chase("*", "*", CHF_DONTDODGE)
			BONE A 1 A_JumpIfCloser(350,"Melee")
			Loop
		Melee:
			BONE A 1 A_SpawnItemEx("SkeletonBoss", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERPOINTERS)
			Stop
	}
}

actor SkeletonBoss : WolfensteinMonster 
{
	missilefrequency 0.02
	health 10
	speed 4, 6
	sighttime 1, 4
	seesound "guard/skeleton"
	meleerange 52
	-CANUSEWALLS
	scale 1.1
	states
	{
		Spawn:
			SKLN P 0 A_Playsound("guard/skeleton")
			SKLN PONM 7.5
			Goto Path
		Path:
			SKLN B 10 NOP A_Chase
			SKLN B 2.5
			SKLN C 7.5 NOP A_Chase
			SKLN D 10 NOP A_Chase
			SKLN D 2.5
			SKLN E 7.5 NOP A_Chase
			loop
		Missile:
			SKLN FGHFG 3 A_FaceTarget
			SKLN G 0 A_JumpIfCloser(50,"Melee")
			Goto See
		Melee:
			SKLN FGH 3 A_FaceTarget
			SKLN I 3 A_FaceTarget
			SKLN J 3 A_MeleeAttack(30,30,"player/hurt","sword/swing")
			SKLN K 3
			SKLN K 0 A_JumpIfCloser(50,"Melee")
			Goto See
		See:
			SKLN B 5 NOP A_Chase
			SKLN B 1.5
			SKLN C 4 NOP A_Chase
			SKLN D 5 NOP A_Chase
			SKLN D 1.5
			SKLN E 4 NOP A_Chase
			loop
		Pain:
			SKLN R 5 A_CustomMissile("Hurt")
			goto Melee
		Death:
			SKLN M 0 A_Playsound("guard/skeleton")
			SKLN M 7.5 A_Fall
			SKLN NOP 7.5
			BONE A 100
			BONE A 0 A_SpawnItemEx("SkeletonBoss", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERPOINTERS)
			stop
		XDeath:
			SKLN M 0 A_Playsound("guard/skeleton")
			SKLN M 7.5 A_Fall
			SKLN NOP 7.5
			BONE A 0 Trigger_Execute(1,1)
			BONE A -1
			stop
	}
}