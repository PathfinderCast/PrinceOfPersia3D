actor ThePrince : PlayerPawn
{
	health 100
	radius 22
	player.movebob 0.3
	player.viewheight 28
	player.displayname "Prince"
	player.startitem "NullWeapon" //In actual game the player starts with no weapon, equip player level start with sword using ensureinventory in mapinfo
	player.damagescreencolor "FF 00 00"
}

actor NullWeapon : WolfWeapon replaces Pistol
{
	inventory.icon "PISTOL"
	weapon.selectionorder 3700
	weapon.ammouse1 0
	+WEAPON.NOAUTOFIRE
	+WEAPON.NOALERT
	states
	{
		Ready:
			BLNK A 1 A_WeaponReady
			loop
		Fire:
			BLNK A 0
			goto Ready
	}
}

actor Sword : WolfWeapon replaces MachineGun
{
	inventory.icon "KNIFE"
	weapon.ammouse1 0
	weapon.selectionorder 1300
	attacksound "sword/swing"
	weapon.bobrangex 1
    weapon.bobrangey 0.2
    weapon.bobspeed 2
    weapon.bobstyle "Smooth"
	+WEAPON.NOGRIN
	weapon.ammotype1 "Magic"
	weapon.ammouse1 0
	weapon.ammogive1 0
	+WEAPON.NOAUTOFIRE
	-WEAPON.DONTBOB
	+WEAPON.NOALERT
	+INVENTORY.ALWAYSPICKUP
	states
	{
		Spawn:
			SORD A 20
			SORD B 4
			loop
		Ready:
			SWRD A 0 A_JumpIfInventory("Magic",1,2)
			SWRD A 1 A_WeaponReady
			loop
			CGSD AHIJ 3 A_WeaponReady
			loop
		Fire:
			SWRD A 0 A_JumpIfInventory("Magic",1,10)
			SWRD BCDEF 2
			SWRD G 3 A_CustomPunch(1, true, CPF_ALWAYSPLAYSOUND, "BulletPuff", 72)
			SWRD FCB 2
			goto Ready
			CGSD BCDEF 2
			CGSD G 0 A_TakeInventory("Magic",1)
			CGSD G 0 A_FireCustomMissile("SwordBlast",0,1,0)
			CGSD G 3 A_CustomPunch(1, true, CPF_ALWAYSPLAYSOUND, "BulletPuff", 72)
			CGSD FCB 2
			goto Ready
	}
}

actor Magic : Ammo 59 //Replaces Clips
{
	inventory.amount 1
	inventory.maxamount 10
	ammo.backpackamount 10
	ammo.backpackboostamount 0
	ammo.backpackmaxamount 10
	states
	{
		Spawn:
			POTD ABCDEFG 3
			loop
	}
}

actor SwordBlast
{
	radius 8
	speed 18
	damage 3
	seesound "missile/fire"
	deathsound "misc/crash"
	PROJECTILE
	states
	{
		Spawn:
			PLPR AB 1.5 bright A_SpawnItemEx("SwordSmoke", random(-8,8), random(-8,8), 0, 0, 0, 0, 0, SXF_TRANSFERPOINTERS)
			loop
		Death:
			PLPR CDE 3 bright
			stop
	}
}

actor SwordSmoke
{
	states
	{
		Spawn:
			PLPR F 0 A_Jump(180, 4)
			PLPR FGH 1.5
			stop
			PLPR F 0 A_Jump(180, 4)
			PLP2 FGH 1.5
			stop
			PLPR F 0 A_Jump(180, 4)
			PLP3 FGH 1.5
			stop
			PLPR F 0 A_Jump(180, 4)
			PLP4 FGH 1.5
			stop
			PLPR F 0 A_Jump(180, 4)
			PLP5 FGH 1.5
			stop
			PLPR F 0 A_Jump(180, 4)
			PLP6 FGH 1.5
			stop
			PLPR F 0 A_Jump(180, 4)
			PLP7 FGH 1.5
			stop
			PLP8 FGH 1.5
			stop
	}
}

actor PersiaRun Replaces BJRun
{
	// GROSS HACK
	radius 80
	speed 4, 1.328
	states
	{
		Spawn:
			BLAZ A 6 NOP A_Chase("*", "*", CHF_NOSIGHTCHECK)
			BLAZ A 1.5
			BLAZ B 4 NOP A_Chase("*", "*", CHF_NOSIGHTCHECK)
			BLAZ C 6 NOP A_Chase("*", "*", CHF_NOSIGHTCHECK)
			BLAZ C 1.5
			BLAZ D 4 NOP A_Chase("*", "*", CHF_NOSIGHTCHECK)
			loop
		Death:
			BLAZ E 7
			BLAZ F 7 A_PlaySound("misc/yeah")
			BLAZ G 7
			BLAZ H 150 A_Stop
			BLAZ H -1 Exit_Normal
			stop
	}
}

